#!/usr/bin/perl -w
use strict;
use File::Basename;
use Getopt::Std;
my $PROGRAM = basename $0;
my $USAGE=
"Usage: $PROGRAM
-o: other disks (tmpfs and squashfs)
-a: all
";

my %OPT;
getopts('oa', \%OPT);

my $COMMAND = "df -h";
if ($^O eq "linux") {
    $COMMAND .= " -T"
}

my @line = `$COMMAND @ARGV`;
chomp(@line);
    
print change_columns(sort_by_size(@line));

################################################################################
### Functions ##################################################################
################################################################################

sub sort_by_size {
    my @line = @_;

    my @size = (0);
    my $i_root = 0;
    my $size_pos = get_column_end_pos($line[0], "Size");
    my $mounted_pos = get_column_start_pos($line[0], "Mounted on");
    for (my $i=1; $i<@line; $i++) {
	my $mounted = substr($line[$i], $mounted_pos);
        if ($mounted eq "/") {
            $i_root = $i;
            next;
        }
	my $fs_info = substr($line[$i], 0, $size_pos);
	if ($fs_info =~ /.* (\S+)$/) {
	    push @size, get_size($1, 1024);
	} else {
	    die;
	}
    }

    my @out = ($line[0]);
    push @out, splice(@line, $i_root, 1);

    my @order = sort { $size[$b]<=>$size[$a] } 1..$#line;
    
    for my $i (@order) {
	push @out, $line[$i];
    }

    return @out;
}

sub change_columns {
    my (@line) = @_;

    my $size_pos = get_column_end_pos($line[0], "Size");
    my $used_pos = get_column_end_pos($line[0], "Used");
    my $avail_pos = get_column_end_pos($line[0], "Avail");
    my $mounted_pos = get_column_start_pos($line[0], "Mounted on");

    my @filtered = ();
    my $max_len = length($line[0]);
    my $min_pos = length($line[0]);
    for my $line (@line) {
	my $fs_info = substr($line, 0, $size_pos);
	my $mounted = substr($line, $mounted_pos);
        if (!$OPT{a} && !$OPT{o}) {
            if ($fs_info =~ /tmpfs|squashfs/ && $mounted ne "/dev/shm" || $mounted =~ /^\/boot/) {
                next;
            }
        }
        if ($OPT{o}) {
            if ($fs_info !~ /tmpfs|squashfs/ && $fs_info !~ /^Filesystem/ && $mounted !~ /^\/boot/) {
                next;
            }
        }

        if (length($line) > $max_len) {
            $max_len = length($line);
        }

	if ($fs_info =~ /(.* )\S+$/) {
	    my $columns = $1;
	    if (length($columns) < $min_pos) {
		$min_pos = length($columns);
	    }
	} else {
	    die;
	}

	push @filtered, $line;
    }

    my @out = ();
    for my $line (@filtered) {
	my $fs_info = substr($line, 0, $min_pos);
	my $size = substr($line, $min_pos, $size_pos - $min_pos);
	my $used = substr($line, $size_pos, $used_pos - $size_pos);
	# my $avail = substr($line, $used_pos, $avail_pos - $used_pos);
	my $percent = substr($line, $avail_pos, $mounted_pos - $avail_pos);
	my $mounted = substr($line, $mounted_pos);
	my $padd = " " x ($max_len - length($line) + 1);
        my $out = "${mounted}${padd} ${size}${used}${percent} ${fs_info}";
	$out =~ s/ +$//;
	push @out, "$out\n";
    }

    return @out;
}

sub get_column_start_pos {
    my ($line, $column) = @_;
    
    my $column_pos;
    if ($line =~ /^(.* +)$column/) {
	$column_pos = length($1);
    } else {
	die;
    }

    return $column_pos;
}

sub get_column_end_pos {
    my ($line, $column) = @_;
    
    my $column_pos;
    if ($line =~ /^(.* +$column)/) {
	$column_pos = length($1);
    } else {
	die;
    }

    return $column_pos;
}

sub get_size {
    my ($size, $base) = @_;

    if ($size =~ /(\S*\d)(\S+)/) {
	my ($num, $unit) = ($1, $2);
	return get_multiplied_size($num, $unit, $base);
    } else {
	return $size;
    }
}

sub get_multiplied_size {
    my ($num, $unit, $base) = @_;
    
    my $multiple = 1;

    if ($unit =~ /K/) {
	$multiple *= $base
    } elsif ($unit =~ /M/) {
	$multiple *= $base ** 2
    } elsif ($unit =~ /G/) {
	$multiple *= $base ** 3
    } elsif ($unit =~ /T/) {
	$multiple *= $base ** 4
    } elsif ($unit =~ /P/) {
	$multiple *= $base ** 5
    }

    return $num * $multiple;
}
