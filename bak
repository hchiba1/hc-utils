#!/usr/bin/perl -w
use strict;
use File::Basename;
use Getopt::Std;
my $PROGRAM = basename $0;
my $USAGE=
"Usage: $PROGRAM FILE...
mv FILE FILE.YY-MM-DD
-c: cp -a FILE FILE.YY-MM-DD
-e .ext: use specified extention
-a EXT: add further extention
-x: preserve extension automatically
-f: force backup
-d: debug (print without execution)
";

my %OPT;
getopts('ce:a:xfd', \%OPT);

if (!@ARGV) {
    print STDERR $USAGE;
    exit 1;
}

for my $file (@ARGV) {
    if (! -e $file) {
        print STDERR "No such file or directory: $file\n";
        next;
    }

    if ($file =~ /\/$/) {
        $file =~ s/\/$//;
    }

    my $new_name = get_new_name($file);

    if (! $OPT{f}) {
        if (-e $new_name) {
            print STDERR "Destination exists: $new_name\n";
            next;
        }
    }

    my $command_line = "mv $file $new_name";
    if ($OPT{c}) {
        $command_line = "cp -a $file $new_name";
    }
    print "> $command_line\n";
    if (!$OPT{d}) {
        my $ret = system($command_line);
        $ret = $ret >> 8;
        if ($ret != 0) {
            die;
        }
    }
}

################################################################################
### Function ###################################################################
################################################################################

sub get_new_name {
    my ($file) = @_;

    my @date = localtime((stat($file))[9]);
    my $year = $date[5] + 1900;
    my $month = $date[4] + 1;
    my $day = $date[3];
    unless ($year =~ /^[12]\d{3}$/) {
        die;
    }
    unless (1 <= $month and $month <= 12) {
        die;
    }
    unless (1 <= $day and $day <= 31) {
        die;
    }

    my $core_name = $file;
    my $ext = '';
    if ($file =~ /^(.*)(\.[a-z]{2,3})$/) {
        $core_name = $1;
        $ext = $2;
    }

    my $new_name = sprintf("${core_name}.%04d-%02d-%02d", $year, $month, $day);
    if ($ext) {
        $new_name .= $ext;
    }
    if ($OPT{e}) {
        $new_name = "$file$OPT{e}";
    }
    if ($OPT{a}) {
        $new_name .= $OPT{a};
    }
    if ($OPT{x}) {
        if ($file =~ /^(\w.*)\.(\w+)$/) {
            my ($prefix, $suffix) = ($1, $2);
            $new_name = sprintf("$prefix.%04d-%02d-%02d.$suffix", $year, $month, $day);
        }
    }
    return $new_name;
}
