#!/usr/bin/perl -w
use strict;
use Getopt::Std;
use File::Basename;
my $program = basename $0;

my $usage = 
"Usage: $program PATTERN [FILE ...]
If file not specified, read from STDIN.
-d DELIM : delimiter ('>' by default)
-D : use 1st line as delimiter
-v : inverse
-w : word match
";

my %opt;
getopts('vd:Dw',\%opt);

# Get pattern
!@ARGV and print($usage), exit(1);
my $pattern = shift @ARGV;
!@ARGV && -t and print($usage), exit(1);

# Set delimiter.
my $sep = $opt{d} || "\n>";
if ($opt{D}) {
    $sep = <>;
}
(my $without_newlines = $sep) =~ s/^\n*//;

$/= $sep;
my $count = 0;
my $first_line = 1;
while(<>){
    chomp;
    # If first line, remove the separator at the beginning of the line.
    if ($first_line) {
	s/^$without_newlines//;
	$first_line = 0;
    }

    my $matched = $opt{w} ? /\b$pattern\b/ : /$pattern/;
    if ($matched && !$opt{v}||
	!$matched && $opt{v}) {
        if (/\n$/) {
            print "$without_newlines$_";
        } else {
            print "$without_newlines$_\n";
        }
	$count ++;
    }
}
exit $count;
